#version 110

varying vec4 coord;

void main(void) {
	gl_Position = ftransform();
    coord = gl_TextureMatrix[0] * gl_MultiTexCoord0;
    coord.y = 1.0 - coord.y;
}

%% -- FRAG
#version 110

#define POINTS 120
// #define INTERPOLATE

varying vec4 coord;
uniform vec3 color_low;
uniform vec3 color_mid;
uniform vec3 color_high;
uniform float points[POINTS];

float sample(float x) {
    x = clamp(x, 0.0, 1.0) * float(POINTS - 1);

    float f = floor(x);
    float y0 = points[int(f)];

#ifdef INTERPOLATE
    float c = ceil(x);
    float y1 = points[int(c)];
    float fract = x - f;
    return y1 * fract + y0 * (1.0 - fract);
#else
    return y0;
#endif
}

void main(void) {
    vec4 c = vec4(1.0);
    float s = sample(coord.x);
    c.a = float(coord.y <= s);
    c.a *= (0.2 + 0.8 * s);
    c.a = 0.05 + 0.95 * c.a;

    float cval = s; // coord.y;

    if(cval > 0.5) {
        c.rgb = mix(color_mid, color_high, 2.0 * (cval - 0.5));
    } else {
        c.rgb = mix(color_low, color_mid,  2.0 *  cval);
    }

    c = mix(c, vec4(1.0), 0.5 * pow(1.0 - abs(coord.y - 0.5), 32.0));
    gl_FragColor = c;
}
